plugins {
  id 'java'
  id 'org.springframework.boot' version '2.6.6'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'com.diffplug.spotless' version '6.4.2'
  id 'org.sonarqube' version '3.3'
  id 'jacoco'
}

group = 'com.mingyuchoo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
  developmentOnly
  runtimeClasspath {
    extendsFrom developmentOnly
  }
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-data-rest'
  implementation 'io.springfox:springfox-data-rest:2.9.2'
  implementation 'io.springfox:springfox-swagger2:2.9.2'
  implementation 'io.springfox:springfox-swagger-ui:2.9.2'
  runtimeOnly 'org.postgresql:postgresql'
  annotationProcessor 'org.projectlombok:lombok'
  compileOnly 'org.projectlombok:lombok'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
  testImplementation 'org.junit.jupiter:junit-jupiter-api'
  testImplementation 'org.junit.jupiter:junit-jupiter-params'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

sonarqube {
  properties {
    property 'sonar.sourceEncoding', 'UTF-8'
  }
}

spotless {
  java {
    importOrder 'java', 'javax', 'org', 'com', ''
    removeUnusedImports()
    googleJavaFormat('1.7').aosp()
    target '**/*.java'
    endWithNewline()
    indentWithSpaces()
    trimTrailingWhitespace()
  }
  groovyGradle {
    target '**/*.gradle'
    greclipse().configFile('greclipse.properties')
    endWithNewline()
    indentWithSpaces(2)
    trimTrailingWhitespace()
  }
  format 'misc', {
    target '**/*.md', '**/.gitignore'
    endWithNewline()
    indentWithSpaces(2)
    trimTrailingWhitespace()
  }
}

test {
  dependsOn 'spotlessApply'
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}

jacoco {
  toolVersion = '0.8.8'
}

jacocoTestReport {
  dependsOn test
}
