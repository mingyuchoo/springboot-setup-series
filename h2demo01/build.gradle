plugins {
  id 'org.springframework.boot' version '2.6.6'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'org.asciidoctor.convert' version '1.5.8'
  id 'com.diffplug.spotless' version '6.4.2'
  id 'org.sonarqube' version '3.3'
  id 'jacoco'
  id 'java'
}

group = 'com.mingyuchoo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

ext {
  set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-data-rest'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  annotationProcessor 'org.projectlombok:lombok'
  compileOnly 'org.projectlombok:lombok'
  runtimeOnly 'com.h2database:h2'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
  testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

test {
  dependsOn 'spotlessApply'
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}

asciidoctor {
  inputs.dir snippetsDir
  dependsOn test
}

sonarqube {
  properties {
    property 'sonar.sourceEncoding', 'UTF-8'
  }
}

jacoco {
  toolVersion = '0.8.8'
}

jacocoTestReport {
  dependsOn test
}

spotless {
  java {
    importOrder 'java', 'javax', 'org', 'com', ''
    removeUnusedImports()
    googleJavaFormat('1.12.0').aosp()
    target '**/*.java'
    endWithNewline()
    indentWithSpaces()
    trimTrailingWhitespace()
  }
  groovyGradle {
    target '**/*.gradle'
    greclipse().configFile('greclipse.properties')
    endWithNewline()
    indentWithSpaces(2)
    trimTrailingWhitespace()
  }
  format 'misc', {
    target '**/*.md', '**/.gitignore'
    endWithNewline()
    indentWithSpaces(2)
    trimTrailingWhitespace()
  }
}
